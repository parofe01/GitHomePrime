<!DOCTYPE html>
<html>
<head>
    <meta charset="utf-8">
    <title>Cascos - Phong Shading (por fragmento)</title>
    <style type="text/css">
        canvas { border: 1px solid black; width: 600px; height: 600px; }
    </style>

    <!-- VERTEX: NO calcula iluminación, sólo pasa normal y posición en espacio ojo -->
    <script id="myVertexShader" type="x-shader/x-vertex">#version 300 es
  uniform mat4 modelViewMatrix;
  uniform mat4 projectionMatrix;
  uniform mat3 normalMatrix;

  in vec3 VertexPosition;
  in vec3 VertexNormal;

  out vec3 vN;
  out vec3 vEC;

  void main() {
    vec4 ecPosition = modelViewMatrix * vec4(VertexPosition, 1.0);
    vEC = ecPosition.xyz;
    vN  = normalize(normalMatrix * VertexNormal);

    gl_Position = projectionMatrix * ecPosition;
  }
    </script>

    <!-- FRAGMENT: Phong por fragmento -->
    <script id="myFragmentShader" type="x-shader/x-fragment">#version 300 es
  precision mediump float;

  in vec3 vN;
  in vec3 vEC;

  out vec4 fragmentColor;

  struct LightData {
    vec3 Position; // en coordenadas del ojo
    vec3 La;
    vec3 Ld;
    vec3 Ls;
  };
  uniform LightData Light;

  struct MaterialData {
    vec3 Ka;
    vec3 Kd;
    vec3 Ks;
    float alpha;
  };
  uniform MaterialData Material;

  vec3 phong(vec3 N, vec3 L, vec3 V) {
    vec3 ambient = Material.Ka * Light.La;
    vec3 diffuse = vec3(0.0);
    vec3 specular = vec3(0.0);

    float NdotL = dot(N, L);
    if (NdotL > 0.0) {
      vec3 R = reflect(-L, N);
      float RdotV_n = pow(max(0.0, dot(R, V)), Material.alpha);
      diffuse  = NdotL * (Light.Ld * Material.Kd);
      specular = RdotV_n * (Light.Ls * Material.Ks);
    }
    return ambient + diffuse + specular;
  }

  void main() {
    vec3 N = normalize(vN);
    vec3 L = normalize(Light.Position - vEC);
    vec3 V = normalize(-vEC);

    vec3 color = phong(N, L, V);
    fragmentColor = vec4(color, 1.0);
  }
    </script>
</head>

<body>
<canvas id="myCanvas" width="600" height="600">
    Tu navegador no soporta HTML5
</canvas>

<script src="https://cdnjs.cloudflare.com/ajax/libs/gl-matrix/2.4.0/gl-matrix.js"></script>
<script src="phong.js"></script>
</body>
</html>
