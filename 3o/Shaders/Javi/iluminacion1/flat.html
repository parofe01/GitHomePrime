<!DOCTYPE html>
<html>
<head>
    <meta charset="utf-8">
    <title>Cascos - Flat Shading</title>
    <style type="text/css">
        canvas { border: 1px solid black; width: 600px; height: 600px; }
    </style>

    <!-- VERTEX: calcula iluminación, pero con 'flat' para NO interpolar el color -->
    <script id="myVertexShader" type="x-shader/x-vertex">#version 300 es
  uniform mat4 modelViewMatrix;
  uniform mat4 projectionMatrix;
  uniform mat3 normalMatrix;

  in vec3 VertexPosition;
  in vec3 VertexNormal;

  flat out vec3 colorOut;

  struct LightData {
    vec3 Position; // en coordenadas del ojo
    vec3 La;
    vec3 Ld;
    vec3 Ls;
  };
  uniform LightData Light;

  struct MaterialData {
    vec3 Ka;
    vec3 Kd;
    vec3 Ks;
    float alpha;
  };
  uniform MaterialData Material;

  vec3 phong(vec3 N, vec3 L, vec3 V) {
    vec3 ambient = Material.Ka * Light.La;
    vec3 diffuse = vec3(0.0);
    vec3 specular = vec3(0.0);

    float NdotL = dot(N, L);
    if (NdotL > 0.0) {
      vec3 R = reflect(-L, N);
      float RdotV_n = pow(max(0.0, dot(R, V)), Material.alpha);
      diffuse  = NdotL * (Light.Ld * Material.Kd);
      specular = RdotV_n * (Light.Ls * Material.Ks);
    }
    return ambient + diffuse + specular;
  }

  void main() {
    vec3 N = normalize(normalMatrix * VertexNormal);

    vec4 ecPosition = modelViewMatrix * vec4(VertexPosition, 1.0);
    vec3 ec = ecPosition.xyz;

    vec3 L = normalize(Light.Position - ec);
    vec3 V = normalize(-ec);

    colorOut = phong(N, L, V);

    gl_Position = projectionMatrix * ecPosition;
  }
    </script>

    <!-- FRAGMENT: recibe colorOut FLAT (sin interpolación) -->
    <script id="myFragmentShader" type="x-shader/x-fragment">#version 300 es
  precision mediump float;

  flat in vec3 colorOut;
  out vec4 fragmentColor;

  void main() {
    fragmentColor = vec4(colorOut, 1.0);
  }
    </script>
</head>

<body>
<canvas id="myCanvas" width="600" height="600">
    Tu navegador no soporta HTML5
</canvas>

<script src="https://cdnjs.cloudflare.com/ajax/libs/gl-matrix/2.4.0/gl-matrix.js"></script>
<script src="flat.js"></script>
</body>
</html>
